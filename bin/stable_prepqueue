#!/usr/bin/perl -w -I /home/latest/repos/slaby-scripts/perl
use strict;
use Error qw(:try);
use Git;
use StableHelper;

die "invalid arguments" if (scalar @ARGV < 2);

my $sdir = shift @ARGV;
my $srepo = Git->repository($sdir);
my $repo = Git->repository();
my $newer = qr/3\.(1[3-9]|[2-9][0-9])|[4-9]\.|[1-9][0-9]\./;
my $myver = '3.12';

my @history = $srepo->command('log', '--format=%B', "v$myver..stable-$myver");
@history = StableHelper::get_upstream_commits(@history);
my %history;
@history{@history} = undef;

my %further;
my %maybe_remove;
my %localSHAs;
my @removed;

sub remove($$) {
	my ($msg, $file) = @_;
	print $msg, " ", scalar $repo->command('rm', $file);
	push @removed, $file;
}

foreach my $patch (@ARGV) {
	open(FILE, "<", $patch) || die "cannot open $patch";
	my @header = ();
	while (my $line = <FILE>) {
		chomp $line;
		last if $line eq "---";
		push @header, $line;
	}
	close FILE;

	# remove duplicates
	my @SHAs = StableHelper::get_upstream_commits(@header);
	if (scalar @SHAs == 1 && exists $history{$SHAs[0]}) {
		remove("DUP", $patch);
		next;
	}

	# remove patches for later stables only
	if (scalar @SHAs == 1) {
		my @stabletags;
		git_cmd_try {
			@stabletags = grep(/^cc:.*stable@.*[\s#]v?$newer/i,
				$srepo->command('show', '--format=%B', '-q',
					$SHAs[0]));
		} "SHA $SHAs[0] does not exist for $patch";
		if (scalar @stabletags == 1 && $stabletags[0] !~ /$myver/) {
			remove("LATER", $patch);
			next;
		}
	}

	# lines with "Fixes: ..."
	my @fixes = map { /fixes.*\s([0-9a-f]{8,})/i ? ($1) : ()  } @header;
	if (scalar @fixes) {
		my $remove = 1;
		foreach my $fix (@fixes) {
			$fix = $srepo->command_oneline('rev-parse', $fix);
			if (exists $history{$fix}) {
				$remove = 0;
				last;
			}
			my $desc = $srepo->command_oneline('describe',
					'--exact-match', '--contains', $fix);
			if ($desc !~ /^v$newer/) {
				$remove = 0;
				last;
			}
		}
		if ($remove) {
			$maybe_remove{$patch} = [ @fixes ];
		}
	}

	$further{$patch} = 1;
	foreach my $sha (@SHAs) {
		$localSHAs{$sha} = $patch;
	}
}

whole:
foreach my $patch (keys %maybe_remove) {
	my $fixes = $maybe_remove{$patch};
	foreach my $fix (@$fixes) {
		if (exists $localSHAs{$fix}) {
			print "not removing $patch as it fixes local $localSHAs{$fix}\n";
			next whole;
		}
	}

	remove("FIX", $patch);
	delete $further{$patch};
}

if (scalar keys %further) {
	system('sed', '-i', 's#Signed-off-by: Greg Kroah-Hartman <gregkh@linuxfoundation.org>#Signed-off-by: Jiri Slaby <jslaby@suse.cz>#',
		keys %further) == 0 or die "cannot exec sed";
}

my $rmregexp = join '|', @removed;

open(FILE, "<", "series") || die "cannot open series for r";
my @series = <FILE>;
close FILE;

@series = grep(!/^$rmregexp$/, @series);
if (scalar @series) {
	open(FILE, ">", 'series') || die "cannot open series for w";
	print FILE @series;
	close FILE;
} else {
	$repo->command_noisy('rm', 'series');
}

1;
